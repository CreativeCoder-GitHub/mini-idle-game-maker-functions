Let's make a game!
  name:Ascension System
  author:CreativeCoder
  desc:A simple ascension system for Idle Game Maker. You may use with or without credit. :)

Settings
  no bulk particles

// The losable tag states that this will be reset on ascension. You can use this to clear anything you would like during ascension.
// In this demo, cash is what is converted into heavenly resources.

Resources
  *cash
    name:Cash
    tag:losable

  *prestige
    name:Prestige
    hidden when 0
    desc:With <b>Legacy</b> bought, every 1 prestige grant +1% MpS.

  *heavenlyCash
    name:Heavenly Cash
    hidden when 0
    desc:Use this to buy <b>special</b> Heavenly upgrades.

  *cashThisAcension
    name:Cash earned this Ascension
    desc:This is the counter of how much cash you made this acension.
    is always:((cash:earned) - cashForfeited)
    tag:losable

  *cashForfeited
    always hidden

  *ascensions
    name:Ascensions
    desc:This shows how many times you have ascended while gaining one prestige. All number of ascension achievements use this number.

  // A value used in calculating remaining cash till next prestige level.
  *N
    is always:floor(1+pow(cashThisAcension/1e12, 1/3))

Buttons
  *resButton
    on click:yield 100 cash
    class:hasFlares
    no tooltip

Upgrades
  // The description is how it should work if you were to expand upon this.
  // Of course, you could change this if you want.
  // Make sure to define heavenly upgrades before the template tag:losable. Otherwise, players will lose heavenly upgrades after ascending.
  *legacyUpgr
    name:Legacy
    desc:Gives +1% CpS for every 1 prestige you have. Also unlocks other heavenly upgrades.
    on tick:multiply cash yield of :Buildings by 1 + ((prestige) / 100)
    cost:1 heavenlyCash
    // On tick gives +1% CpS for every 1 prestige you have.

*TEMPLATE
tag:losable

  *Upgrade1
    desc:Multiplies efficiency of Building1 by 2.
    passive:multiply yield of Building1 by 2
  
  *Upgrade2
    desc:Multiplies efficiency of Building2 by 2.
    passive:multiply yield of Building2 by 2

  *Upgrade12
    name:Upgrade1.2
    desc:Multiplies efficiency of Building1 by 2.
    passive:multiply yield of Building1 by 2
  
  *Upgrade22
    name:Upgrade2.2
    desc:Multiplies efficiency of Building2 by 2.
    passive:multiply yield of Building2 by 2

  *acension
    name:Ascend
    desc:Ascend to a higher state of being. You will lose all your building and non-heavenly upgrades and all your money will be converted to Prestige and Heavenly Cash.</>Ascending know will give you <b>[floor(pow(cashThisAcension/1e12, 1/3))]</b> prestige and heavenly cash. </> You need <b>[(N*N*N*1e12)-(cashThisAcension)]</b> cash for <b>+1 prestige and heavenly cash<b>.

    on earn:
      yield (cashThisAcension) cashForfeited
      yield floor(pow(cashThisAcension/1e12, 1/3)) prestige
      yield floor(pow(cashThisAcension/1e12, 1/3)) heavenlyCash
      if (floor(pow(cashThisAcension/1e12, 1/3)) > 0) yield 1 ascensions
      lose tag:losable
    end

Buildings
*TEMPLATE
tag:losable
  *Building1
    desc:Makes 100 CpS at the start.(this number does not scale with upgrades).
    on tick:yield 100 cash
  
  *Building2
    desc:Makes 1 trillion CpS at the start.(this number does not scale with upgrades).
    on tick:yield 1e12 cash

Achievements
  *cash1
    name:Money!!!
    desc:Make 1 million cash.
    req:(ascensions) >= 1
  *ascend1
    name:Rebirth
    desc:Ascend at least once.
    req:(ascensions) >= 1
  *ascend2
    name:Resurrection
    desc:Ascend <b>10 times</b>.
    req:(ascensions) >= 10
  *ascend3
    name:Reincarnation
    desc:Ascend <b>100 times</b>.
    req:(ascensions) >= 100
  *ascend4
    name:Endless cycle
    desc:Ascend <b>1,000 times</b>.<q><i>"Oh hey, it's you again."</i></q>
    req:(ascensions) >= 1000
  *ascend5
    name:The afterlife
    desc:Ascend <b>1 million times</b>.<q><i>"At this point, most of your time is spent thinking about pressing Ascend."</i></q>
    req:(ascensions) >= 1000000
